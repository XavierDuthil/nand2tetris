// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs.
 * Which function to compute is determined by 6 input bits 
 * denoted zx, nx, zy, ny, f, no.
 * The computed function's value is called "out".
 * In addition to computing out, the ALU computes two 
 * 1-bit outputs called zr and ng:
 * if out == 0, zr = 1; otherwise zr = 0;
 * If out < 0, ng = 1; otherwise ng = 0.
 * The 6-bit combinations (zx,nx,zy,ny,f,no) and 
 * their effect are documented in the book. 
 */
 
// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx  == 1) sets x = 0        // 16-bit constant
// if (nx  == 1) sets x = ~x       // bitwise "not"
// if (zy  == 1) sets y = 0        // 16-bit constant
// if (ny  == 1) sets y = ~y       // bitwise "not"
// if (f   == 1) sets out = x + y  // integer 2's-complement addition
// if (f   == 0) sets out = x & y  // bitwise And
// if (no  == 1) sets out = ~out   // bitwise Not
// if (out == 0) sets zr = 1
// if (out < 0)  sets ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if f == 1) or out = x & y (if == 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
	// GESTION DE L'ENTREE X
	// Condition ZX
	Mux16(a=x, sel=zx, out=X1);
	
	// Condition NX
	Not16(in=X1, out=nonX);
	Mux16(a=X1, b=nonX, sel=nx, out=XFinal);
	
	// GESTION DE L'ENTREE Y
	// Condition ZY
	Mux16(a=y, sel=zy, out=Y1);
	
	// Condition NY
	Not16(in=Y1, out=nonY);
	Mux16(a=Y1, b=nonY, sel=ny, out=YFinal);
	
	// GESTION DU CALCUL
	Add16(a=XFinal, b=YFinal, out=outAdd);
	And16(a=XFinal, b=YFinal, out=outAnd);
	Mux16(a=outAnd, b=outAdd, sel=f, out=outReel);
	
	// GESTION DU RESULTAT
	Not16(in=outReel, out=nonOutReel);
	Mux16(a=outReel, b=nonOutReel, sel=no, out=outFinal, out=out, out[15]=ng);
	
	// GESTION DES SORTIES SECONDAIRES
	ZeroDetector16(in=outFinal, out=zr);
}
